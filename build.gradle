plugins {
	id 'java'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

sourceCompatibility = 8
targetCompatibility = 8

group = 'org.unmojang'
archivesBaseName = 'Loki'
version = '1.6.3'

repositories {
	maven {
		url 'https://repo.sleeping.town'
		content {
			includeGroup 'com.unascribed'
		}
	}
}

dependencies {
	implementation 'com.unascribed:nilloader:1.3.6'
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:-deprecation" << "-Xlint:rawtypes" << "-Xlint:all" << "-Xlint:-options"
}

tasks.processResources {
    filesMatching('**/*.nilmod.css') {
        expand(version: project.version)
    }
}

shadowJar {
    archiveClassifier.set('') // remove '-all' suffix
    dependencies {
        include(dependency('com.unascribed:nilloader'))
    }

    manifest {
        attributes(
            'Premain-Class': 'nilloader.NilAgent',
            'Agent-Class': 'nilloader.NilAgent',
            'Can-Retransform-Classes': 'true'
        )
    }
}

tasks.named('jar') {
    enabled = false
}

tasks.named('build') {
    dependsOn shadowJar
}