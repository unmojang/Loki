plugins {
	id 'java'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

sourceCompatibility = 8
targetCompatibility = 8

group = 'org.unmojang'
archivesBaseName = 'Loki'
version = '1.8.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.grack:nanojson:1.10"
    implementation "org.ow2.asm:asm:9.9"
    implementation "org.ow2.asm:asm-tree:9.9"
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:-deprecation" << "-Xlint:rawtypes" << "-Xlint:all" << "-Xlint:-options"
}

tasks.processResources {
    filesMatching('**/*.nilmod.css') {
        expand(version: project.version)
    }
}

shadowJar {
    archiveClassifier.set('') // remove '-all' suffix
    dependencies {
        include(dependency('com.grack:nanojson'))
        include(dependency('org.ow2.asm:asm'))
        include(dependency('org.ow2.asm:asm-tree'))
    }
    minimize()
    exclude('META-INF/maven/**')

    relocate('com.grack.nanojson', 'org.unmojang.loki.internal.nanojson')
    relocate('org.objectweb.asm', 'org.unmojang.loki.internal.asm')

    manifest {
        attributes(
            'Premain-Class': 'org.unmojang.loki.Loki',
            'Can-Redefine-Classes': 'true',
            'Can-Retransform-Classes': 'true',
            'Implementation-Version': project.version
        )
    }
}

tasks.named('jar') {
    enabled = false
}

tasks.named('build') {
    dependsOn shadowJar
}