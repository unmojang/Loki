plugins {
	id 'com.unascribed.nilgradle' version '1.4.4'
}

sourceCompatibility = 8
targetCompatibility = 8

group = 'com.unmojang'
archivesBaseName = 'Loki'
version = '1.0'

repositories {
	maven {
		url 'https://repo.sleeping.town'
		content {
			includeGroup 'com.unascribed'
		}
	}
}

nil {
	id "loki"
	// Set this to false for a smaller JAR that can't be run as a standalone Java agent
	standalone true
}

dependencies {
	loader 'com.unascribed:nilloader:1.3.6'
	game nil.minecraft('1.4.7', 'client')
}

mappings {
	work nil.mcpArchive('1.4.7', '726a')
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xlint:rawtypes" << "-Xlint:all"
}

task compileJava16WrappingProvider(type: JavaCompile) {
	source = fileTree('src/java16') // Only for WrappingProvider
	destinationDir = file("$buildDir/classes/java16")
	classpath = sourceSets.main.runtimeClasspath
	sourceCompatibility = 16
	targetCompatibility = 16

	javaCompiler = javaToolchains.compilerFor {
		languageVersion = JavaLanguageVersion.of(16)
	}
}

jar {
	from(sourceSets.main.output)
	from("$buildDir/classes/java16")
}

jar.dependsOn compileJava16WrappingProvider